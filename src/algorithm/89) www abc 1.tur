МТ: L = {www | w ∈ {a, b, c}*}
abc
1

# ПОДСЧЁТ СИМВОЛОВ В СЛОВЕ (ДОЛЖНО БЫТЬ КРАТНО 3)
# Почти то же, что и q1, но отдельно обрабатываем случай, когда входное слово пустое.
q0 λ -> qz 1 E
q0 a -> q2 A R
q0 b -> q2 B R
q0 c -> q2 C R

# Начинаем процесс подсчёта символов в слове слева направо. Проверенные символы переводим верхний регистр.
q1 a -> q2 A R
q1 b -> q2 B R
q1 c -> q2 C R
q1 A -> q1 A R
q1 B -> q1 B R
q1 C -> q1 C R
q1 * -> q1 * R
q1 λ -> q5 λ L  # Если проверены все символы, возвращаемся в начало слова для начала следующего этапа.

# Второй шаг процесса.
q2 a -> q3 A R
q2 b -> q3 B R
q2 c -> q3 C R
q2 λ -> q22 λ L

# Третий шаг процесса, после которого нужно вернуться в начало слова.
q3 a -> q4 A L
q3 b -> q4 B L
q3 c -> q4 C L
q3 λ -> q22 λ L

# Возвращаемся в начало слова, ставим отметку о найденной тройке символов и повторяем процесс.
q4 A -> q4 A L
q4 B -> q4 B L
q4 C -> q4 C L
q4 * -> q4 * L
q4 λ -> q1 * R


# РАЗДЕЛЕНИЕ СЛОВА НА 3 ЧАСТИ
# Переводим последние заглавные символы в нижний регистр, чтобы разделить 3 части слова.
q5 A -> q6 a L
q5 B -> q6 b L
q5 C -> q6 c L

# Идём в начало слова.
q6 A -> q6 A L
q6 B -> q6 B L
q6 C -> q6 C L
q6 a -> q7 a R  # Как только встречаем строчной символ, меняем стоящий справа от него тоже на строчной.
q6 b -> q7 b R
q6 c -> q7 c R
q6 * -> q7 * R

# Переводим первые заглавные символы в нижний регистр и идём в конец слова.
q7 A -> q7 a L
q7 B -> q7 b L
q7 C -> q7 c L
q7 a -> q7 a L
q7 b -> q7 b L
q7 c -> q7 c L
q7 * -> q7 * L
q7 λ -> q8 λ R

# Убираем отметку о найденной тройке.
q8 * -> q9 λ R

# Если остались отметки *, возвращаемся в конец слова. Иначе — переходим к следующим этапам (в зависимости от букв состояния разные).
q9 * -> q10 * R
q9 a -> q12 λ R
q9 b -> q13 λ R
q9 c -> q14 λ R
q9 x -> q16 λ R
q9 y -> q17 λ R
q9 z -> q18 λ R

# Идём до тех пор, пока не встретим заглавный символ.
q10 * -> q10 * R
q10 A -> q11 A R
q10 B -> q11 B R
q10 C -> q11 C R
q10 a -> q10 a R
q10 b -> q10 b R
q10 c -> q10 c R

# Проходим заглавные символы, а когда встречаем строчные — повторяем процесс.
q11 A -> q11 A R
q11 B -> q11 B R
q11 C -> q11 C R
q11 a -> q5 a L
q11 b -> q5 b L
q11 c -> q5 c L


# СОПОСТАВЛЕНИЕ 1 И 2 ЧАСТЕЙ
# Идём вправо, пока не встретим символ A. Когда встретим, заменяем на x и переходим к следующему этапу.
q12 a -> q12 a R
q12 b -> q12 b R
q12 c -> q12 c R
q12 x -> q12 x R
q12 y -> q12 y R
q12 z -> q12 z R
q12 B -> q21 B R  # Если же встретили другой заглавный символ, очищаем ленту с отрицательным результатом.
q12 C -> q21 C R
q12 A -> q15 x L

# То же самое для B.
q13 a -> q13 a R
q13 b -> q13 b R
q13 c -> q13 c R
q13 x -> q13 x R
q13 y -> q13 y R
q13 z -> q13 z R
q13 A -> q21 A R
q13 C -> q21 C R
q13 B -> q15 y L

# То же самое для C.
q14 a -> q14 a R
q14 b -> q14 b R
q14 c -> q14 c R
q14 x -> q14 x R
q14 y -> q14 y R
q14 z -> q14 z R
q14 B -> q21 B R
q14 A -> q21 A R
q14 C -> q15 z L

# Идём в начало слова, чтобы после этого в зависимости от символа выбрать нужное состояние в q9.
q15 a -> q15 a L
q15 b -> q15 b L
q15 c -> q15 c L
q15 x -> q15 x L
q15 y -> q15 y L
q15 z -> q15 z L
q15 * -> q20 λ R  # Если же символов не осталось, а остались только *, значит все символы сопоставлены и слово принадлежит языку.
q15 λ -> q9 λ R


# СОПОСТАВЛЕНИЕ 2 И 3 ЧАСТЕЙ
# Идём вправо, пока не встретим a.
q16 x -> q16 x R
q16 y -> q16 y R
q16 z -> q16 z R
q16 * -> q16 * R
q16 b -> q16 b R
q16 c -> q16 c R
q16 a -> q19 * L  # Если встретили, то заменяем её на * и идём в начало слова.

# То же самое для b.
q17 x -> q17 x R
q17 y -> q17 y R
q17 z -> q17 z R
q17 * -> q17 * R
q17 a -> q17 a R
q17 c -> q17 c R
q17 b -> q19 * L

# То же самое для c.
q18 x -> q18 x R
q18 y -> q18 y R
q18 z -> q18 z R
q18 * -> q18 * R
q18 b -> q18 b R
q18 a -> q18 a R
q18 c -> q19 * L

# Почти дубликат q15.
q19 a -> q19 a L
q19 b -> q19 b L
q19 c -> q19 c L
q19 x -> q19 x L
q19 y -> q19 y L
q19 z -> q19 z L
q19 * -> q19 * L  # Отличие здесь, потому что нужно и звёзды пройти тоже.
q19 λ -> q15 λ R

# ОЧИСТКА ЛЕНТЫ
# С положительным результатом.
q20 * -> q20 λ R
q20 λ -> qz 1 E

# C отрицательным результатом.
# Двигаемся к концу слова, заменяя всё на *.
q21 a -> q21 * R
q21 b -> q21 * R
q21 c -> q21 * R
q21 x -> q21 * R
q21 y -> q21 * R
q21 z -> q21 * R
q21 A -> q21 * R
q21 B -> q21 * R
q21 C -> q21 * R
q21 * -> q21 * R
q21 λ -> q22 λ L

# И очищаем ленту.
q22 a -> q22 λ L
q22 b -> q22 λ L
q22 c -> q22 λ L
q22 x -> q22 λ L
q22 y -> q22 λ L
q22 z -> q22 λ L
q22 A -> q22 λ L
q22 B -> q22 λ L
q22 C -> q22 λ L
q22 * -> q22 λ L
q22 λ -> qz 0 E
