МТ: L = {(ad)^n (cb)^2n | n ≥ 1}
abcd
1

q0 λ -> qz 0 E    # Если слово пустое, завершаем с результатом 0
q0 a -> q2 λ R    # Если слово начинается с a, проверяем след. букву на d
q0 b -> q10 λ R   # Иначе — q10
q0 c -> q10 λ R
q0 d -> q10 λ R

q1 * -> q8 λ R    # Если ad больше нет, проверяем наличие cbcb
q1 a -> q2 λ R    # Если встретилась a, проверяем след. букву на d

q2 d -> q3 λ R    # Если встретилась d, проверяем след. букву на c
q2 λ -> qz 0 E    # Если вместо d встретился конец слова, завершаем с результатом 0
q2 a -> q10 λ R   # Иначе — q10
q2 b -> q10 λ R
q2 c -> q10 λ R

q3 * -> q3 * R    # Если встретились a, d или *, их нужно пройти до попадания в cbcb
q3 a -> q3 a R
q3 d -> q3 d R
q3 c -> q4 * R    # Если встретилась с, проверяем след. букву на b
q3 b -> q11 b R   # Иначе переходим в конец слова
q3 λ -> q12 λ L   # и очищаем влево с результатом 0

q4 b -> q5 * R    # Если встретилась b, проверяем след. букву на c
q4 a -> q11 a R   # Иначе переходим в конец слова
q4 c -> q11 c R
q4 d -> q11 d R
q4 λ -> q12 λ L   # и очищаем влево с результатом 0

q5 c -> q6 * R    # Если встретилась с, проверяем след. букву на b
q5 a -> q11 a R   # Иначе переходим в конец слова
q5 b -> q11 b R
q4 d -> q11 d R
q5 λ -> q12 λ L   # и очищаем влево с результатом 0

q6 b -> q7 * L    # Если встретилась b, переходим в начало слова
q6 a -> q11 a R   # Иначе переходим в конец слова
q6 c -> q11 a R
q6 d -> q11 a R
q6 λ -> q12 λ L   # и очищаем влево с результатом 0

q7 * -> q7 * L    # Переходим в начало слова, чтобы вновь начать сопоставление пар
q7 a -> q7 a L
q7 d -> q7 d L
q7 λ -> q1 λ R

q8 * -> q8 λ R    # Переходим в конец слова, чтобы проверить, есть ли несопоставленные cbcb
q8 λ -> qz 1 E    # Если их нет, завершаем с результатом 1
q8 a -> q10 λ R   # Иначе — q10
q8 b -> q10 λ R
q8 c -> q10 λ R
q8 d -> q10 λ R

q10 λ -> qz 0 E   # Очистка вправо с результатом 0
q10 * -> q10 λ R
q10 a -> q10 λ R
q10 b -> q10 λ R
q10 c -> q10 λ R
q10 d -> q10 λ R

q11 λ -> q12 λ L  # Переход в конец слова с переходом в q12
q11 a -> q11 a R
q11 b -> q11 b R
q11 c -> q11 c R
q11 d -> q11 d R

q12 λ -> qz 0 E   # Очистка влево с результатом 0
q12 * -> q12 λ L
q12 a -> q12 λ L
q12 b -> q12 λ L
q12 c -> q12 λ L
q12 d -> q12 λ L
