МТ: L = {ww | w ∈ {a, b, c}*}
abc
1

q0 λ -> qz 1 E
q0 a -> q1 x R   # Проходим 1 ячейку с заменой букв на заменители (x, y, z)
q0 b -> q1 y R
q0 c -> q1 z R
q1 a -> q2 x L   # Проходим 2 ячейку с заменой букв на их заменители
q1 b -> q2 y L
q1 c -> q2 z L
q1 λ -> q14 λ L  # Если букв нечётное количество, очищаем слово влево
q2 * -> q2 * L   # Идём в начало слова, где
q2 x -> q2 x L
q2 y -> q2 y L
q2 z -> q2 z L
q2 λ -> q3 * R   # приписываем обозначение парного символа (*)
q3 x -> q3 x R   # Затем идём в конец слова, пропуская заменители
q3 y -> q3 y R
q3 z -> q3 z R
q3 * -> q3 * R
q3 ! -> q3 ! R
q3 a -> q1 x R   # Проходим 1 ячейку с заменой букв на заменители (и переходим на 2)
q3 b -> q1 y R
q3 c -> q1 z R
q3 λ -> q4 λ L   # Если все буквы заменены,

q4 a -> q4 a L   # идём в начало слова
q4 b -> q4 b L
q4 c -> q4 c L
q4 x -> q4 x L
q4 y -> q4 y L
q4 z -> q4 z L
q4 * -> q4 * L
q4 λ -> q5 λ R   # до самого первого признака парного символа
q5 a -> q7 λ R   # Если * нет, переходим к сравнению букв
q5 b -> q9 λ R
q5 c -> q11 λ R
q5 ! -> q15 λ R  # Если и буквы закончились, очищаем ленту с положительным исходом
q5 * -> q6 λ R   # Если * есть, 
q6 a -> q6 a R   # идём до первой заменённой буквы
q6 b -> q6 b R
q6 c -> q6 c R
q6 * -> q6 * R
q6 x -> q4 a R   # меняем заменитель на букву, после чего снова идём в начало слова
q6 y -> q4 b R
q6 z -> q4 c R

q7 a -> q7 a R   # Сравниваем букву а
q7 b -> q7 b R   # Для этого идём во вторую половину слова к первому заменителю
q7 c -> q7 c R
q7 ! -> q7 ! R
q7 y -> q13 ! R  # Если заменитель не соответствует букве а,
q7 z -> q13 ! R  # очищаем ленту с отрицательным результатом
q7 x -> q8 ! L   # Иначе
q8 a -> q8 a L   # идём в начало слова
q8 b -> q8 b L
q8 c -> q8 c L
q8 ! -> q8 ! L
q8 λ -> q5 λ R   # и продолжаем сравнение

q9 a -> q9 a R   # Сравниваем букву b (аналогично a)
q9 b -> q9 b R
q9 c -> q9 c R
q9 ! -> q9 ! R
q9 x -> q13 ! R
q9 y -> q10 ! L
q9 z -> q13 ! R
q10 a -> q10 a L
q10 b -> q10 b L
q10 c -> q10 c L
q10 λ -> q5 λ R
q10 ! -> q10 ! L

q11 a -> q11 a R # Сравниваем букву c (аналогично c)
q11 b -> q11 b R
q11 c -> q11 c R
q11 ! -> q11 ! R
q11 x -> q13 ! R
q11 y -> q13 ! R
q11 z -> q12 ! L
q12 a -> q12 a L
q12 b -> q12 b L
q12 c -> q12 c L
q12 λ -> q5 λ R
q12 ! -> q12 ! L

q13 a -> q13 ! R # Проходим ленту вправо
q13 b -> q13 ! R
q13 c -> q13 ! R
q13 x -> q13 ! R
q13 y -> q13 ! R
q13 z -> q13 ! R
q13 ! -> q13 ! R
q13 λ -> q14 λ L
q14 a -> q14 λ L # а потом очисщаем её влево
q14 b -> q14 λ L
q14 c -> q14 λ L
q14 x -> q14 λ L
q14 y -> q14 λ L
q14 z -> q14 λ L
q14 * -> q14 λ L
q14 ! -> q14 λ L
q14 λ -> qz 0 E  # с отрицательным исходом
q15 ! -> q15 λ R # Если все буквы сверены, очищаем ленту вправо
q15 λ -> qz 1 E  # с положительным исходом