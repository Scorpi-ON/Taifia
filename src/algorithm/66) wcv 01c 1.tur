МТ: L = {wcv | w, v ∈ {0, 1}* & v ≠ w & |w| = 2k & |v| = 2n & k, n ≥ 0}
01c
1

q0 1 -> q1 1 R   # Проходим 1 цифру из пары в подслове w
q0 0 -> q1 0 R
q0 c -> q2 c R   # Если w закончилось, и c было найдено, смотрим v
q0 λ -> q21 λ L  # Если w закончилось, а c так и не было найдено, очищаем ленту с отрицательным исходом

q1 1 -> q0 1 R   # Проходим 2 цифру из пары в подслове w
q1 0 -> q0 0 R
q1 c -> q20 ! R  # Если вместо буквы из пары встретилось с, проходим ленту вправо и очищаем её с отрицательным исходом
q1 λ -> q21 λ L  # Если цифр нечётное количество, очищаем ленту влево

q2 1 -> q3 1 R   # Проходим 1 цифру из пары в подслове v
q2 0 -> q3 0 R
q2 c -> q20 ! R
q2 λ -> q4 λ L   # Если v закончилось, переходим к сравнению его с w

q3 1 -> q2 1 R   # Проходим 2 цифру из пары в подслове w
q3 0 -> q2 0 R
q3 c -> q20 ! R
q3 λ -> q21 λ L  # Если цифр нечётное количество, очищаем ленту с отрицательным исходом

q4 1 -> q6 λ L   # Сравниваем каждую из цифр с конца v
q4 0 -> q11 λ L
q4 c -> q5 λ L  # Если v пустое, надо проверить, пустое ли w

q5 λ -> qz 0 E  # Если w пустое, то слова одинаковые — с отрицательный исход
q5 1 -> q23 λ L # иначе — положительный
q5 0 -> q23 λ L



q6 1 -> q6 1 L   # Сравнение цифры 1 начинается с прохода w влево
q6 0 -> q6 0 L
q6 c -> q7 c L

q7 ! -> q7 ! L   # Проходим проверенные цифры в w
q7 1 -> q8 ! L   # Если 1 совпал, проверим, есть ли в w ещё цифры для сверки
q7 0 -> q17 ! L  # Если обнаружилось несовпадение, проходим ленту влево и очищаем с положительныим исходом
q7 λ -> q18 λ R

q8 1 -> q9 1 R   # Если в w есть ещё цифры для сверки, идём обратно в конец v
q8 0 -> q9 0 R
q8 λ -> q16 λ R  # Если же w закончилось, нужно посмотреть, пусто ли v

q9 ! -> q9 ! R   # Идём к подслову v
q9 c -> q9 c R
q9 λ -> q23 λ L  # Если v пустое, то слова разные — очистка с положительным исходом
q9 1 -> q10 1 R   # Если не пустое,
q9 0 -> q10 0 R

q10 1 -> q10 1 R   # Проходим v до конца
q10 0 -> q10 0 R
q10 λ -> q4 λ L   # После чего начинаем сравнение снова



q11 1 -> q11 1 L # Сравнение цифры 0 (аналогично q6 — q10)
q11 0 -> q11 0 L
q11 c -> q12 c L

q12 ! -> q12 ! L
q12 1 -> q17 ! L
q12 0 -> q13 ! L
q12 λ -> q18 λ R

q13 1 -> q9 1 R
q13 0 -> q14 0 R
q13 λ -> q16 λ R

q14 ! -> q14 ! R
q14 c -> q14 c R
q14 1 -> q15 1 R
q14 0 -> q15 0 R
q14 λ -> q23 λ L

q15 1 -> q15 1 R
q15 0 -> q15 0 R
q15 λ -> q4 λ L



q16 ! -> q16 ! R  # После окончания w идём к подслову v, чтобы проверить, пусто ли оно
q16 c -> q16 c R
q16 λ -> q21 λ L  # Если v пустое, то слова одинаковые — очистка с отрицательным исходом
q16 1 -> q22 ! R  # иначе — с положительным
q16 0 -> q22 ! R

q17 1 -> q17 ! L # Проходим ленту влево
q17 0 -> q17 ! L
q17 λ -> q18 λ R
q18 1 -> q18 λ R # а потом очищаем её вправо с положительным исходом
q18 0 -> q18 λ R
q18 c -> q18 λ R
q18 ! -> q18 λ R
q18 λ -> qz 1 E

q19 1 -> q19 λ R # Очищаем ленту вправо с отрицательным исходом
q19 0 -> q19 λ R
q19 c -> q19 λ R
q19 ! -> q19 λ R
q19 λ -> qz 0 E

q20 1 -> q20 ! R # Проходим ленту вправо
q20 0 -> q20 ! R
q20 c -> q20 ! R
q20 λ -> q21 λ L
q21 1 -> q21 λ L # а потом очищаем её влево с отрицательным исходом
q21 0 -> q21 λ L
q21 c -> q21 λ L
q21 ! -> q21 λ L
q21 λ -> qz 0 E

q22 1 -> q22 ! R # Проходим ленту вправо
q22 0 -> q22 ! R
q22 λ -> q23 λ L
q23 1 -> q23 λ L # а потом очищаем её влево с положительным исходом
q23 0 -> q23 λ L
q23 c -> q23 λ L
q23 ! -> q23 λ L
q23 λ -> qz 1 E