МТ: L = {w w^R w w^R | w ∈ {a, b, c}*}
abc
1

# ПЕРВОЕ СИММЕТРИЧНОЕ СРАВНЕНИЕ
# Сравниваем первые буквы слова с последними, заменяя их на прописные
q0 λ -> q8 λ L  # Если сравнивать уже нечего, переходим к второму этапу сравнения
q0 a -> q1 A R
q0 b -> q3 B R
q0 c -> q5 C R

# Идём до конца слова, ожидая увидеть там a
q1 a -> q1 a R
q1 b -> q1 b R
q1 c -> q1 c R
q1 λ -> q2 λ L

# Если встретилось a, стираем его и идём в начало слова
q2 a -> q7 λ L
q2 b -> q22 λ L  # Иначе очищаем ленту
q2 c -> q22 λ L
q2 A -> q22 λ L
q2 B -> q22 λ L
q2 C -> q22 λ L

# Аналогично для b
q3 a -> q3 a R
q3 b -> q3 b R
q3 c -> q3 c R
q3 λ -> q4 λ L

q4 b -> q7 λ L
q4 a -> q22 λ L
q4 c -> q22 λ L
q4 A -> q22 λ L
q4 B -> q22 λ L
q4 C -> q22 λ L

# Аналогично для c
q5 a -> q5 a R
q5 b -> q5 b R
q5 c -> q5 c R
q5 λ -> q6 λ L

q6 c -> q7 λ L
q6 a -> q22 λ L
q6 b -> q22 λ L
q6 A -> q22 λ L
q6 B -> q22 λ L
q6 C -> q22 λ L

# Идём в начало слова. Достигая A, B или С, начинаем сравнение снова
q7 a -> q7 a L
q7 b -> q7 b L
q7 c -> q7 c L
q7 A -> q0 A R
q7 B -> q0 B R
q7 C -> q0 C R

# ВТОРОЕ СИММЕТРИЧНОЕ СРАВНЕНИЕ
# Примерно по той же схеме сравниваем крайние символы с обеих сторон
# q8 и q9 (как и другие состояния ниже) делают то же самое, только в противоположные стороны
q8 A -> q10 λ L
q8 B -> q14 λ L
q8 C -> q18 λ L
q8 λ -> qz 1 E

q9 A -> q11 λ R
q9 B -> q15 λ R
q9 C -> q19 λ R
q9 λ -> qz 1 E

# Идём в начало слова, ожидая найти там A
q10 A -> q10 A L
q10 B -> q10 B L
q10 C -> q10 C L
q10 λ -> q12 λ R

# или в конец — в данном случае не важно
q11 A -> q11 A R
q11 B -> q11 B R
q11 C -> q11 C R
q11 λ -> q13 λ L

# Если увидели A, то проделываем такое же сравнение, только в другую сторону
q12 A -> q9 λ R
q12 B -> q23 λ R
q12 C -> q23 λ R
q12 λ -> qz 0 E

q13 A -> q8 λ L
q13 B -> q22 λ L
q13 C -> q22 λ L
q13 λ -> qz 0 E

# Аналогично для B
q14 A -> q14 A L
q14 B -> q14 B L
q14 C -> q14 C L
q14 λ -> q16 λ R

q15 A -> q15 A R
q15 B -> q15 B R
q15 C -> q15 C R
q15 λ -> q17 λ L

q16 B -> q9 λ R
q16 A -> q23 λ R
q16 C -> q23 λ R
q16 λ -> qz 0 E

q17 B -> q8 λ L
q17 A -> q22 λ L
q17 C -> q22 λ L
q17 λ -> qz 0 E

# Аналогично для C
q18 A -> q18 A L
q18 B -> q18 B L
q18 C -> q18 C L
q18 λ -> q20 λ R

q19 A -> q19 A R
q19 B -> q19 B R
q19 C -> q19 C R
q19 λ -> q21 λ L

q20 C -> q9 λ R
q20 A -> q23 λ R
q20 B -> q23 λ R
q20 λ -> qz 0 E

q21 C -> q8 λ L
q21 A -> q22 λ L
q21 B -> q22 λ L
q21 λ -> qz 0 E

# Очистка ленты влево с отрицательным результатом
q22 a -> q22 λ L
q22 b -> q22 λ L
q22 c -> q22 λ L
q22 A -> q22 λ L
q22 B -> q22 λ L
q22 C -> q22 λ L
q22 λ -> qz 0 E

# Очистка ленты вправо с отрицательным результатом
q23 A -> q23 λ R
q23 B -> q23 λ R
q23 C -> q23 λ R
q23 λ -> qz 0 E