ММТ: L = {w w^R w w^R | w ∈ {a, b, c}*}
abc
2

# ПРОХОДИМ ВПРАВО ПО ЛЕНТЕ И КАЖДЫЕ 4 СИМВОЛА ПРОСТАВЛЯЕМ НА 2 ЛЕНТУ ФИЛЛЕР *.
# ПОТОМУ КАК РАБОТАТЬ НАМ НУЖНО С 1/4 ИСХОДНОГО СЛОВА.

# 1 (только с поправкой, что пустое слово принадлежит языку)
q0 λ,λ -> qz 1,λ E,E
q0 a,λ -> q2 a,* R,R
q0 b,λ -> q2 b,* R,R
q0 c,λ -> q2 c,* R,R

# 1
q1 a,λ -> q2 a,* R,R
q1 b,λ -> q2 b,* R,R
q1 c,λ -> q2 c,* R,R
q1 λ,λ -> q5 λ,λ L,L  # Если четвёрки закончились, переходим ко второму этапу

# 2
q2 a,λ -> q3 a,λ R,E
q2 b,λ -> q3 b,λ R,E
q2 c,λ -> q3 c,λ R,E
q2 λ,λ -> q9 λ,λ L,L

# 3
q3 a,λ -> q4 a,λ R,E
q3 b,λ -> q4 b,λ R,E
q3 c,λ -> q4 c,λ R,E
q3 λ,λ -> q9 λ,λ L,L

# 4 (и возврат в 1)
q4 a,λ -> q1 a,λ R,E
q4 b,λ -> q1 b,λ R,E
q4 c,λ -> q1 c,λ R,E
q4 λ,λ -> q9 λ,λ L,L

# ПЕРЕНОСИМ НА 2 ЛЕНТУ ВМЕСТО * УЖЕ НАСТОЯЩИЕ СИМВОЛЫ И НАЧИНАЕМ СРАВНИВАТЬ ИХ С 1 ЛЕНТОЙ.
# В СИЛУ СИММЕТРИИ, ПРОХОД ПО ВТОРОЙ ЛЕНТЕ ВЛЕВО-ВПРАВО ПОЗВОЛИТ СРАВНИТЬ w w^R.

# Проход влево (с переносом)
q5 a,* -> q5 λ,a L,L
q5 b,* -> q5 λ,b L,L
q5 c,* -> q5 λ,c L,L
q5 a,λ -> q6 a,λ E,R
q5 b,λ -> q6 b,λ E,R
q5 c,λ -> q6 c,λ E,R

# Проход вправо
q6 a,a -> q6 λ,a L,R
q6 b,b -> q6 λ,b L,R
q6 c,c -> q6 λ,c L,R
q6 a,b -> q10 λ,λ L,R
q6 a,c -> q10 λ,λ L,R
q6 b,a -> q10 λ,λ L,R
q6 b,c -> q10 λ,λ L,R
q6 c,a -> q10 λ,λ L,R
q6 c,b -> q10 λ,λ L,R
q6 a,λ -> q7 a,λ E,L
q6 b,λ -> q7 b,λ E,L
q6 c,λ -> q7 c,λ E,L

# Проход влево
q7 a,a -> q7 λ,a L,L
q7 b,b -> q7 λ,b L,L
q7 c,c -> q7 λ,c L,L
q7 a,b -> q10 λ,λ L,L
q7 a,c -> q10 λ,λ L,L
q7 b,a -> q10 λ,λ L,L
q7 b,c -> q10 λ,λ L,L
q7 c,a -> q10 λ,λ L,L
q7 c,b -> q10 λ,λ L,L
q7 a,λ -> q8 a,λ E,R
q7 b,λ -> q8 b,λ E,R
q7 c,λ -> q8 c,λ E,R

# Проход вправо
q8 a,a -> q8 λ,λ L,R
q8 b,b -> q8 λ,λ L,R
q8 c,c -> q8 λ,λ L,R
q8 a,b -> q10 λ,λ L,R
q8 a,c -> q10 λ,λ L,R
q8 b,a -> q10 λ,λ L,R
q8 b,c -> q10 λ,λ L,R
q8 c,a -> q10 λ,λ L,R
q8 c,b -> q10 λ,λ L,R
q8 λ,λ -> qz 1,λ E,E

# Очистка лент влево-влево с отрицательным результатом
q9 a,a -> q9 λ,λ L,L
q9 a,b -> q9 λ,λ L,L
q9 a,c -> q9 λ,λ L,L
q9 b,a -> q9 λ,λ L,L
q9 b,b -> q9 λ,λ L,L
q9 b,c -> q9 λ,λ L,L
q9 c,a -> q9 λ,λ L,L
q9 c,b -> q9 λ,λ L,L
q9 c,c -> q9 λ,λ L,L
q9 a,* -> q9 λ,λ L,L
q9 b,* -> q9 λ,λ L,L
q9 c,* -> q9 λ,λ L,L
q9 a,λ -> q9 λ,λ L,E
q9 b,λ -> q9 λ,λ L,E
q9 c,λ -> q9 λ,λ L,E
q9 λ,λ -> qz 0,λ E,E

# Очистка лент влево-вправо с отрицательным результатом
q10 a,a -> q10 λ,λ L,R
q10 a,b -> q10 λ,λ L,R
q10 a,c -> q10 λ,λ L,R
q10 b,a -> q10 λ,λ L,R
q10 b,b -> q10 λ,λ L,R
q10 b,c -> q10 λ,λ L,R
q10 c,a -> q10 λ,λ L,R
q10 c,b -> q10 λ,λ L,R
q10 c,c -> q10 λ,λ L,R
q10 a,λ -> q10 λ,λ L,E
q10 b,λ -> q10 λ,λ L,E
q10 c,λ -> q10 λ,λ L,E
q10 λ,λ -> qz 0,λ E,E
