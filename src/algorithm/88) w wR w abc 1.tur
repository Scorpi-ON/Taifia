МТ: L = {w w^R w | w ∈ {a, b, c}*}
abc
1

# ПОДСЧЁТ СИМВОЛОВ В СЛОВЕ (ДОЛЖНО БЫТЬ КРАТНО 3)
# Почти то же, что и q1, но отдельно обрабатываем случай, когда входное слово пустое.
q0 λ -> qz 1 E
q0 a -> q2 A R
q0 b -> q2 B R
q0 c -> q2 C R

# Начинаем процесс подсчёта символов в слове слева направо. Проверенные символы переводим верхний регистр.
q1 a -> q2 A R
q1 b -> q2 B R
q1 c -> q2 C R
q1 A -> q1 A R
q1 B -> q1 B R
q1 C -> q1 C R
q1 * -> q1 * R
q1 λ -> q5 λ L  # Если проверены все символы, возвращаемся в начало слова для начала следующего этапа.

# Второй шаг процесса.
q2 a -> q3 A R
q2 b -> q3 B R
q2 c -> q3 C R
q2 λ -> q28 λ L

# Третий шаг процесса, после которого нужно вернуться в начало слова.
q3 a -> q4 A L
q3 b -> q4 B L
q3 c -> q4 C L
q3 λ -> q28 λ L

# Возвращаемся в начало слова, ставим отметку о найденной тройке символов и повторяем процесс.
q4 A -> q4 A L
q4 B -> q4 B L
q4 C -> q4 C L
q4 * -> q4 * L
q4 λ -> q1 * R


# РАЗДЕЛЕНИЕ СЛОВА НА 3 ЧАСТИ
# Переводим последние заглавные символы в нижний регистр, чтобы разделить 3 части слова.
q5 A -> q6 a L
q5 B -> q6 b L
q5 C -> q6 c L

# Идём в начало слова.
q6 A -> q6 A L
q6 B -> q6 B L
q6 C -> q6 C L
q6 a -> q7 a R  # Как только встречаем строчной символ, меняем стоящий справа от него тоже на строчной.
q6 b -> q7 b R
q6 c -> q7 c R
q6 * -> q7 * R

# Переводим первые заглавные символы в нижний регистр и идём в конец слова.
q7 A -> q7 a L
q7 B -> q7 b L
q7 C -> q7 c L
q7 a -> q7 a L
q7 b -> q7 b L
q7 c -> q7 c L
q7 * -> q7 * L
q7 λ -> q8 λ R

# Убираем отметку о найденной тройке и переходим к следующему этапу.
q8 * -> q9 λ R

# Если остались отметки *, возвращаемся в конец слова. Иначе — переходим к следующему этапу.
q9 * -> q10 * R
q9 a -> q12 λ R
q9 b -> q14 λ R
q9 c -> q16 λ R

# Идём до тех пор, пока не встретим заглавный символ.
q10 * -> q10 * R
q10 A -> q11 A R
q10 B -> q11 B R
q10 C -> q11 C R
q10 a -> q10 a R
q10 b -> q10 b R
q10 c -> q10 c R

# Проходим заглавные символы, а когда встречаем строчные — повторяем процесс.
q11 A -> q11 A R
q11 B -> q11 B R
q11 C -> q11 C R
q11 a -> q5 a L
q11 b -> q5 b L
q11 c -> q5 c L


# СОПОСТАВЛЕНИЕ 1 И 3 ЧАСТЕЙ
# Удаляем строчные символы из первой части (q9), а их копии во второй части заменяем на x, y и z.
# Для a:
q12 a -> q12 a R
q12 b -> q12 b R
q12 c -> q12 c R
q12 A -> q13 A R
q12 B -> q13 B R
q12 C -> q13 C R

q13 A -> q13 A R
q13 B -> q13 B R
q13 C -> q13 C R
q13 x -> q13 x R
q13 y -> q13 y R
q13 z -> q13 z R
q13 a -> q18 x L
q13 b -> q29 * R
q13 c -> q29 * R

# Для b:
q14 a -> q14 a R
q14 b -> q14 b R
q14 c -> q14 c R
q14 A -> q15 A R
q14 B -> q15 B R
q14 C -> q15 C R

q15 A -> q15 A R
q15 B -> q15 B R
q15 C -> q15 C R
q15 x -> q15 x R
q15 y -> q15 y R
q15 z -> q15 z R
q15 b -> q18 y L
q15 a -> q29 * R
q15 c -> q29 * R

# Для c:
q16 a -> q16 a R
q16 b -> q16 b R
q16 c -> q16 c R
q16 A -> q17 A R
q16 B -> q17 B R
q16 C -> q17 C R

q17 A -> q17 A R
q17 B -> q17 B R
q17 C -> q17 C R
q17 x -> q17 x R
q17 y -> q17 y R
q17 z -> q17 z R
q17 c -> q18 z L
q17 a -> q29 * R
q17 b -> q29 * R

# Возвращаемся в начало слова. Если строчных букв нет, то переходим к следующему этапу.
q18 A -> q18 A L
q18 B -> q18 B L
q18 C -> q18 C L
q18 a -> q19 a L
q18 b -> q19 b L
q18 c -> q19 c L
q18 x -> q18 x L
q18 y -> q18 y L
q18 z -> q18 z L
q18 λ -> q20 λ R

# Если строчные буквы есть, идём в самое начало слова и повторяем процесс (q9).
q19 a -> q19 a L
q19 b -> q19 b L
q19 c -> q19 c L
q19 λ -> q9 λ R


# СОПОСТАВЛЕНИЕ 2 И 3 ЧАСТЕЙ
# Удаляем заглавные символы в начале 2 части и сравниваем их с x, y и z в 3 части, также удаляя.
q20 A -> q21 λ R
q20 B -> q23 λ R
q20 C -> q25 λ R
q20 λ -> qz 1 E  # Если 

# Для A:
q21 A -> q21 A R
q21 B -> q21 B R
q21 C -> q21 C R
q21 x -> q21 x R
q21 y -> q21 y R
q21 z -> q21 z R
q21 λ -> q22 λ L

q22 x -> q27 λ L
q22 y -> q28 λ L
q22 z -> q28 λ L

# Для B:
q23 A -> q23 A R
q23 B -> q23 B R
q23 C -> q23 C R
q23 x -> q23 x R
q23 y -> q23 y R
q23 z -> q23 z R
q23 λ -> q24 λ L

q24 y -> q27 λ L
q24 x -> q28 λ L
q24 z -> q28 λ L

# Для C:
q25 A -> q25 A R
q25 B -> q25 B R
q25 C -> q25 C R
q25 x -> q25 x R
q25 y -> q25 y R
q25 z -> q25 z R
q25 λ -> q26 λ L

q26 z -> q27 λ L
q26 x -> q28 λ L
q26 y -> q28 λ L

# Возвращаемся в начало слова и повторяем процесс.
q27 x -> q27 x L
q27 y -> q27 y L
q27 z -> q27 z L
q27 A -> q27 A L
q27 B -> q27 B L
q27 C -> q27 C L
q27 λ -> q20 λ R


# ОЧИСТКА ЛЕНТЫ С ОТРИЦАТЕЛЬНЫМ РЕЗУЛЬТАТОМ
q28 a -> q28 λ L
q28 b -> q28 λ L
q28 c -> q28 λ L
q28 x -> q28 λ L
q28 y -> q28 λ L
q28 z -> q28 λ L
q28 A -> q28 λ L
q28 B -> q28 λ L
q28 C -> q28 λ L
q28 * -> q28 λ L
q28 λ -> qz 0 E

# Если очистку вызвали не в конце слова, двигаемся к этому концу.
q29 a -> q29 * R
q29 b -> q29 * R
q29 c -> q29 * R
q29 x -> q29 * R
q29 y -> q29 * R
q29 z -> q29 * R
q29 A -> q29 * R
q29 B -> q29 * R
q29 C -> q29 * R
q29 * -> q29 * R
q29 λ -> q28 λ L
